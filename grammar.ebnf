program    = function_gval*
function_gval = TYPE "*"* ident "(" (TYPE "*"* ident)* ")" "{" stmt* "}"
                | TYPE "*"* ident ("[" num "]")* "[]"?
                    ("=" ("{" (num ("," num)*)? "}") | num | string )? ";"
stmt = { stmt* }
        | TYPE "*"* ident ("[" num "]")* "[]"?
            ("=" ("{" (num ("," num))? "}") | num | string )? ";"
        | "return" expr ";"
        | "if" "(" expr ")" stmt ("else" stmt)?
        | "while" "(" expr ")" stmt
        | "for" "(" expr? ";" expr? ";" expr? ")" stmt
        | expr ";"
        | ";"
expr       = comma
comma      = assign ("," assign)*
assign     = logicOR (("=" | "+=" | "-=" | "*=" | "/="
            | "%=" | "&=" | "^=" | "|=" | "<<=" | ">>=") assign)?
logicOR    = logicAND ("||" logicAND)*
logicAND   = bitOR ("&&" bitOR)*
bitOR      = bitXOR ("|" bitXOR)*
bitXOR     = bitAND ("^" bitAND)*
bitAND     = equality ("&" equality)*
equality   = relational ("==" relational | "!=" relational)*
relational = shift (("<" | "<=" | ">" | ">=") shift)*
shift      = add ("<<" add | ">>" add)*
add        = mul ("+" mul | "-" mul)*
mul        = unary (("*" | "/" | "%") unary)*
unary      = ("sizeof" | "&" | "*") unary
            |  ("+" | "-")? brackets
brackets   = primary ("[" expr "]")*
primary    = "(" expr ")" | string
            | ident "(" (assign ("," assign)*)? ")"
            | ident | num
