program    = function_gval*
function_gval = TYPE ident "(" (TYPE ident)* ")" "{" stmt* "}" # 関数定義
                | TYPE ident "(" (TYPE ident)* ")" ";" # プロトタイプ宣言
                | TYPE ident ("[" num "]")* "[]"?
                    ("=" ("{" (num ("," num)*)? "}") | num | string )? ";"
                | "extern" TYPE ident ("[" num "]")*
                | "enum" ident "{" ident ("," ident)* "}" ";"
                | "struct" ident "{" (TYPE ident ";")* "}" ";"
                | "typedef" TYPE ident ";"
stmt = { stmt* }
        | TYPE ident ("[" num "]")* "[]"?
            ("=" ("{" (num ("," num))? "}") | num | string )? ";"
        | "return" expr? ";"
        | "if" "(" expr ")" stmt ("else" stmt)?
        | "switch"  "(" expr ")" "{"
            ((("case" (num | string)) | "default") ":" stmt*)* "}"
        | "while" "(" expr ")" stmt
        | "for" "(" expr? ";" expr? ";" expr? ")" stmt
        | "break" ";"
        | "continue" ";"
        | expr ";"
        | ";"
expr       = comma
comma      = assign ("," assign)*
assign     = condition (("=" | "+=" | "-=" | "*=" | "/="
            | "%=" | "&=" | "^=" | "|=" | "<<=" | ">>=") assign)?
condition  = logicOR ("?" expr ":" condition)? # 中央はカンマ演算子などもOK a?b,c:d → a?(b,c):d と解釈
logicOR    = logicAND ("||" logicAND)*
logicAND   = bitOR ("&&" bitOR)*
bitOR      = bitXOR ("|" bitXOR)*
bitXOR     = bitAND ("^" bitAND)*
bitAND     = equality ("&" equality)*
equality   = relational ("==" relational | "!=" relational)*
relational = shift (("<" | "<=" | ">" | ">=") shift)*
shift      = add ("<<" add | ">>" add)*
add        = mul ("+" mul | "-" mul)*
mul        = unary (("*" | "/" | "%") unary)*
unary      = ("sizeof" | "&" | "*" | # TODO ("(" TYPE ")")
                | "!" | "~" | "++" | "--") unary  # ++ -- は前置
            |  ("+" | "-")? postpos
postpos    = primary (("[" expr "]") || ("." ident) || ("->" ident))*
            ("++" | "--")?  # ++ -- は後置
primary    = "(" expr ")" | string
            | ident "(" (assign ("," assign)*)? ")" # commaとの衝突を避けるためassign
            | ident | num


# TODO:int/char以外の型,配列型や関数ポインタ型の整理
TYPE       = ("int" | "char" | ("struct" ident) | ("enum" ident)
            | definedtype) "*"*
